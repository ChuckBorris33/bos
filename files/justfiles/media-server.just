# Private helper - sources the media server config
source-config:
    #!/usr/bin/env bash
    set -euo pipefail

    # Source the service definitions
    set -a
    if [ -f /usr/etc/bos/media_server/config.sh ]; then
        . /usr/etc/bos/media_server/config.sh
    fi
    set +a

[group('Setup')]
setup-caddy:
    #!/usr/bin/env bash
    set -euxo pipefail

    # Source config with auto-export
    set -a && source /usr/etc/bos/media_server/config.sh && set +a

    echo "Initializing Caddy user and directories..."
    sudo mkdir -p /var/opt/caddy/config /var/opt/caddy/data
    if ! getent group caddy >/dev/null; then sudo groupadd --system --gid 82 caddy; fi
    if ! getent passwd caddy >/dev/null; then sudo useradd --system --gid caddy --uid 82 --no-create-home --shell /bin/false caddy; fi
    sudo chown -R caddy:caddy /var/opt/caddy

    echo "Getting Tailscale IP..."
    if ! MEDIA_SERVER_IP=$(sudo tailscale ip -4 2>/dev/null); then
        echo "Error: Failed to get Tailscale IP. Is Tailscale running?"
        exit 1
    fi
    echo "Found Tailscale IP: ${MEDIA_SERVER_IP}"
    export MEDIA_SERVER_IP

    echo "Generating Caddyfile..."
    TPL_DIR="/usr/etc/bos/media_server_templates"
    sudo mkdir -p /etc/caddy
    sudo -E envsubst < "${TPL_DIR}/Caddyfile.tpl" > /etc/caddy/Caddyfile
    sudo chown -R caddy:caddy /etc/caddy

    echo "Copying Caddy quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "${TPL_DIR}/caddy.container" /etc/containers/systemd/

[group('Setup')]
setup-dashboard:
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Setting up dashboard..."
    sudo mkdir -p /var/www
    sudo cp -r /usr/share/www/dashboard /var/www/
    sudo chown -R caddy:caddy /var/www/dashboard
    sudo chmod 644 /var/www/index.html
    echo "Dashboard installed to /var/www/index.html"

[group('Setup')]
setup-dnsmasq:
    #!/usr/bin/env bash
    set -euxo pipefail

    # Source config with auto-export
    set -a && source /usr/etc/bos/media_server/config.sh && set +a

    echo "Initializing dnsmasq user and directories..."
    sudo mkdir -p /var/opt/dnsmasq/dnsmasq.d
    sudo touch /var/opt/dnsmasq/dnsmasq.custom
    if ! getent group dnsmasq >/dev/null; then sudo groupadd --system --gid 99 dnsmasq; fi
    if ! getent passwd dnsmasq >/dev/null; then sudo useradd --system --gid dnsmasq --uid 99 --no-create-home --shell /bin/false dnsmasq; fi
    sudo chown -R dnsmasq:dnsmasq /var/opt/dnsmasq

    echo "Getting Tailscale IP..."
    if ! MEDIA_SERVER_IP=$(sudo tailscale ip -4 2>/dev/null); then
        echo "Error: Failed to get Tailscale IP. Is Tailscale running?"
        exit 1
    fi
    echo "Found Tailscale IP: ${MEDIA_SERVER_IP}"
    export MEDIA_SERVER_IP

    echo "Generating dnsmasq hosts config..."
    TPL_DIR="/usr/etc/bos/media_server_templates"
    sudo mkdir -p /opt/dnsmasq/dnsmasq.d
    DNSMASQ_CONF_PATH="/opt/dnsmasq/dnsmasq.d/hosts.conf"
    echo "# This file is auto-generated by setup-dnsmasq" | sudo tee "${DNSMASQ_CONF_PATH}" > /dev/null
    echo "# Do not edit manually." | sudo tee -a "${DNSMASQ_CONF_PATH}" > /dev/null
    for domain in ${DNSMASQ_DOMAINS}; do
        echo "address=/${domain}/${MEDIA_SERVER_IP}" | sudo tee -a "${DNSMASQ_CONF_PATH}" > /dev/null
    done
    sudo chown -R dnsmasq:dnsmasq /opt/dnsmasq

    echo "Copying dnsmasq quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo -E envsubst < "${TPL_DIR}/dnsmasq.container.tpl" > /etc/containers/systemd/dnsmasq.container

# Private entrypoint - creates the serverstorage group
_ensure-serverstorage-group:
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing serverstorage group..."
    if ! getent group serverstorage >/dev/null; then sudo groupadd --system --gid 1003 serverstorage; fi

[group('Setup')]
setup-jellyfin: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing Jellyfin user and directories..."
    sudo mkdir -p /var/opt/jellyfin/config /var/opt/jellyfin/cache
    if ! getent group jellyfin >/dev/null; then sudo groupadd --system --gid 1002 jellyfin; fi
    if ! getent passwd jellyfin >/dev/null; then sudo useradd --system --gid jellyfin --uid 1002 --yes-create-home --shell /bin/false jellyfin; fi
    sudo chown -R jellyfin:jellyfin /var/opt/jellyfin
    if [ -f /etc/jellyfin/authentication.xml ]; then
        sudo cp /etc/jellyfin/authentication.xml /var/opt/jellyfin/config/authentication.xml
        sudo chown jellyfin:jellyfin /var/opt/jellyfin/config/authentication.xml
    fi

    echo "Adding jellyfin to serverstorage group..."
    sudo usermod -aG serverstorage jellyfin

    echo "Copying Jellyfin quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/jellyfin.container" /etc/containers/systemd/

[group('Setup')]
setup-filebrowser: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing File Browser user and directories..."
    sudo mkdir -p /var/opt/filebrowser
    sudo mkdir -p /var/opt/filebrowser/config
    sudo mkdir -p /var/opt/filebrowser/database
    if ! getent group filebrowser >/dev/null; then sudo groupadd --system --gid 1001 filebrowser; fi
    if ! getent passwd filebrowser >/dev/null; then sudo useradd --system --gid filebrowser --uid 1001 --no-create-home --shell /bin/false filebrowser; fi
    sudo chown -R filebrowser:filebrowser /var/opt/filebrowser

    echo "Adding filebrowser to serverstorage group..."
    sudo usermod -aG serverstorage filebrowser

    echo "Copying File Browser quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/filebrowser.container" /etc/containers/systemd/

[group('Setup')]
setup-beszel:
    #!/usr/bin/env bash
    set -euxo pipefail

    # Source config with auto-export
    set -a && source /usr/etc/bos/media_server/config.sh && set +a

    TPL_DIR="/usr/etc/bos/media_server_templates"

    echo "Initializing Beszel user and directories..."
    sudo mkdir -p /var/opt/beszel/data
    if ! getent group beszel >/dev/null; then sudo groupadd --system --gid 1004 beszel; fi
    if ! getent passwd beszel >/dev/null; then sudo useradd --system --gid beszel --uid 1004 --no-create-home --shell /bin/false beszel; fi
    sudo chown -R beszel:beszel /var/opt/beszel

    echo "Copying Beszel quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/beszel.container" /etc/containers/systemd/

    echo "Configuring Beszel Agent service patterns..."
    AGENT_SERVICE_FILE="/etc/systemd/system/beszel-agent.service"
    if [ -f "${AGENT_SERVICE_FILE}" ]; then
        echo "Found beszel-agent.service, configuring service patterns..."
        AGENT_OVERRIDE_DIR="${AGENT_SERVICE_FILE}.d"
        sudo mkdir -p "${AGENT_OVERRIDE_DIR}"
        sudo -E envsubst < "${TPL_DIR}/10-service-patterns.conf" > "${AGENT_OVERRIDE_DIR}/10-service-patterns.conf"
    fi

[group('Setup')]
setup-metube: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing Metube user and directories..."
    if ! getent group metube >/dev/null; then sudo groupadd --system --gid 1006 metube; fi
    if ! getent passwd metube >/dev/null; then sudo useradd --system --gid metube --uid 1006 --no-create-home --shell /bin/false metube; fi

    echo "Adding metube to serverstorage group..."
    sudo usermod -aG serverstorage metube

    echo "Copying Metube quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/metube.container" /etc/containers/systemd/

[group('Setup')]
setup-fsqd: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing fsqd user and directories..."
    sudo mkdir -p /var/opt/fsqd/data
    if ! getent group fsqd >/dev/null; then sudo groupadd --system --gid 1008 fsqd; fi
    if ! getent passwd fsqd >/dev/null; then sudo useradd --system --gid fsqd --uid 1008 --no-create-home --shell /bin/false fsqd; fi
    sudo chown -R fsqd:fsqd /var/opt/fsqd

    echo "Adding fsqd to serverstorage group..."
    sudo usermod -aG serverstorage fsqd

    echo "Copying fsqd quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/fsqd.container" /etc/containers/systemd/

[group('Setup')]
setup-samba: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing Samba user and directories..."
    if ! getent group samba >/dev/null; then sudo groupadd --system --gid 1007 samba; fi
    if ! getent passwd samba >/dev/null; then sudo useradd --system --gid samba --uid 1007 --no-create-home --shell /bin/false samba; fi

    echo "Adding samba to serverstorage group..."
    sudo usermod -aG serverstorage samba

    echo "Copying Samba quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/samba.container" /etc/containers/systemd/

[group('Setup')]
setup-audiobookshelf: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing Audiobookshelf user and directories..."
    sudo mkdir -p /var/opt/audiobookshelf/config /var/opt/audiobookshelf/metadata
    if ! getent group audiobookshelf >/dev/null; then sudo groupadd --system --gid 1009 audiobookshelf; fi
    if ! getent passwd audiobookshelf >/dev/null; then sudo useradd --system --gid audiobookshelf --uid 1009 --no-create-home --shell /bin/false audiobookshelf; fi
    sudo chown -R audiobookshelf:audiobookshelf /var/opt/audiobookshelf

    echo "Adding audiobookshelf to serverstorage group..."
    sudo usermod -aG serverstorage audiobookshelf

    echo "Creating and setting permissions for audiobooks directory..."
    sudo mkdir -p /var/server_storage/audiobooks
    sudo chown -R audiobookshelf:serverstorage /var/server_storage/audiobooks
    sudo chmod -R u=rwX,g=rwsX,o-rwx /var/server_storage/audiobooks

    echo "Copying Audiobookshelf quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/audiobookshelf.container" /etc/containers/systemd/

[group('Setup')]
setup-sonarr: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing Sonarr user and directories..."
    sudo mkdir -p /var/opt/sonarr/config
    if ! getent group sonarr >/dev/null; then sudo groupadd --system --gid 1011 sonarr; fi
    if ! getent passwd sonarr >/dev/null; then sudo useradd --system --gid sonarr --uid 1011 --no-create-home --shell /bin/false sonarr; fi
    sudo chown -R sonarr:sonarr /var/opt/sonarr

    echo "Adding sonarr to serverstorage group..."
    sudo usermod -aG serverstorage sonarr

    echo "Copying Sonarr quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/sonarr.container" /etc/containers/systemd/

[group('Setup')]
setup-radarr: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing Radarr user and directories..."
    sudo mkdir -p /var/opt/radarr/config
    if ! getent group radarr >/dev/null; then sudo groupadd --system --gid 1012 radarr; fi
    if ! getent passwd radarr >/dev/null; then sudo useradd --system --gid radarr --uid 1012 --no-create-home --shell /bin/false radarr; fi
    sudo chown -R radarr:radarr /var/opt/radarr

    echo "Adding radarr to serverstorage group..."
    sudo usermod -aG serverstorage radarr

    echo "Copying Radarr quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/radarr.container" /etc/containers/systemd/

[group('Setup')]
setup-gluetun:
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing Gluetun directories..."
    sudo mkdir -p /var/opt/gluetun

    CREDENTIALS_FILE="/var/opt/gluetun/credentials.env"
    if [ -f "$CREDENTIALS_FILE" ]; then
        if gum confirm "Gluetun credentials already exist. Do you want to update them?"; then
            echo "Updating Gluetun credentials..."
            USERNAME=$(gum input --placeholder "Enter OpenVPN username" --prompt "Username: ")
            PASSWORD=$(gum input --password --placeholder "Enter OpenVPN password" --prompt "Password: ")
            echo "OPENVPN_USER=$USERNAME" | sudo tee "$CREDENTIALS_FILE" > /dev/null
            echo "OPENVPN_PASSWORD=$PASSWORD" | sudo tee -a "$CREDENTIALS_FILE" > /dev/null
            sudo chmod 600 "$CREDENTIALS_FILE"
            echo "Credentials updated securely to $CREDENTIALS_FILE"
        else
            echo "Keeping existing credentials."
        fi
    else
        echo "Setting up Gluetun credentials..."
        USERNAME=$(gum input --placeholder "Enter OpenVPN username" --prompt "Username: ")
        PASSWORD=$(gum input --password --placeholder "Enter OpenVPN password" --prompt "Password: ")
        echo "OPENVPN_USER=$USERNAME" | sudo tee "$CREDENTIALS_FILE" > /dev/null
        echo "OPENVPN_PASSWORD=$PASSWORD" | sudo tee -a "$CREDENTIALS_FILE" > /dev/null
        sudo chmod 600 "$CREDENTIALS_FILE"
        echo "Credentials saved securely to $CREDENTIALS_FILE"
    fi

    echo "Copying Gluetun quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/gluetun.container" /etc/containers/systemd/

[group('Setup')]
setup-qbittorrent: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Initializing qBittorrent user and directories..."
    sudo mkdir -p /var/opt/qbittorrent/config
    if ! getent group qbittorrent >/dev/null; then sudo groupadd --system --gid 1010 qbittorrent; fi
    if ! getent passwd qbittorrent >/dev/null; then sudo useradd --system --gid qbittorrent --uid 1010 --no-create-home --shell /bin/false qbittorrent; fi
    sudo chown -R qbittorrent:qbittorrent /var/opt/qbittorrent

    echo "Adding qbittorrent to serverstorage group..."
    sudo usermod -aG serverstorage qbittorrent

    echo "Copying qBittorrent scripts..."
    sudo mkdir -p /var/opt/scripts
    sudo cp "/usr/etc/bos/media_server_templates/scripts/wait-for-vpn.sh" /var/opt/scripts/
    sudo chmod +x /var/opt/scripts/wait-for-vpn.sh

    echo "Copying qBittorrent quadlet container file..."
    sudo mkdir -p /etc/containers/systemd
    sudo cp "/usr/etc/bos/media_server_templates/qbittorrent.container" /etc/containers/systemd/

[group('Setup')]
setup-server-storage: _ensure-serverstorage-group
    #!/usr/bin/env bash
    set -euxo pipefail

    if [ ! -d /var/server_storage ]; then
        echo "Creating Btrfs subvolume at /var/server_storage..."
        sudo btrfs subvolume create /var/server_storage
    fi

    echo "Creating storage directories..."
    sudo mkdir -p /var/server_storage/media /var/server_storage/games /var/server_storage/downloads

    echo "Setting permissions for storage directories..."
    sudo chown -R filebrowser:serverstorage /var/server_storage
    sudo chmod -R u=rwX,g=rwsX,o-rwx /var/server_storage

    echo "Initializing WWW directory..."
    sudo mkdir -p /var/www
    if [ -d "/usr/share/www" ]; then
        sudo cp -r /usr/share/www/* /var/www/
    fi

[group('Setup')]
setup-media-services:
    #!/usr/bin/env bash
    set -euxo pipefail

    echo "Setting up media services..."
    echo "This will configure Jellyfin, Caddy, and other media services."

    ujust setup-server-storage
    ujust setup-caddy
    ujust setup-dnsmasq
    ujust setup-jellyfin
    ujust setup-filebrowser
    ujust setup-beszel
    ujust setup-metube
    ujust setup-fsqd
    ujust setup-gluetun
    ujust setup-samba
    ujust setup-audiobookshelf
    ujust setup-qbittorrent
    ujust setup-sonarr
    ujust setup-radarr

    ujust restart-media-services
    echo "Media services setup complete!"

[group('Setup')]
setup-media-services-interactive:
    #!/usr/bin/env bash
    set -euxo pipefail

    choices=$(gum choose --no-limit \
        "setup-server-storage" \
        "setup-caddy" \
        "setup-dnsmasq" \
        "setup-jellyfin" \
        "setup-filebrowser" \
        "setup-beszel" \
        "setup-metube" \
        "setup-fsqd" \
        "setup-gluetun" \
        "setup-samba" \
        "setup-audiobookshelf" \
        "setup-qbittorrent" \
        "setup-sonarr" \
        "setup-radarr")

    for choice in $choices; do
        ujust $choice
    done

    ujust restart-media-services
    echo "Selected media services setup complete!"

[group('Debug')]
restart-media-services:
    #!/usr/bin/env bash
    set -euxo pipefail
    echo "Restarting media services..."
    sudo systemctl daemon-reload

    # Stop VPN-dependent services first (so gluetun can restart)
    echo "Stopping VPN-dependent services..."
    sudo systemctl stop qbittorrent.service || true
    sudo systemctl stop sonarr.service || true
    sudo systemctl stop radarr.service || true

    # Restart non-VPN services
    sudo systemctl restart dnsmasq.service || true
    sudo systemctl restart caddy.service || true
    sudo systemctl restart jellyfin.service || true
    sudo systemctl restart filebrowser.service || true
    sudo systemctl restart beszel.service || true
    sudo systemctl restart metube.service || true
    sudo systemctl restart samba.service || true
    sudo systemctl restart fsqd.service || true
    sudo systemctl restart audiobookshelf.service || true

    # Restart gluetun
    echo "Restarting gluetun VPN..."
    if sudo systemctl restart gluetun.service; then
        echo "Gluetun restarted successfully. Restarting VPN-dependent services..."
        sudo systemctl restart qbittorrent.service || true
        sudo systemctl restart sonarr.service || true
        sudo systemctl restart radarr.service || true
    else
        echo "ERROR: gluetun failed to restart. VPN-dependent services (qbittorrent, sonarr, radarr) are stopped."
        exit 1
    fi

    echo "Media services restarted!"

[group('Setup')]
setup-pegasus-kiosk:
    #!/usr/bin/env bash
    set -euxo pipefail

    TPL_DIR="/usr/etc/bos/media_server_templates"
    KIOSK_USER="steam"

    # Setup Pegasus configuration
    echo "Setting up Pegasus configuration..."
    PEGASUS_CONFIG_DIR="/home/$KIOSK_USER/.config/pegasus-frontend"
    PEGASUS_THEME_DIR="$PEGASUS_CONFIG_DIR/themes"
    sudo mkdir -p "$PEGASUS_CONFIG_DIR" "$PEGASUS_THEME_DIR"

    # Copy Pegasus settings
    sudo cp "${TPL_DIR}/pegasus/settings.txt" "$PEGASUS_CONFIG_DIR/settings.txt"

    # Copy Steam collection metadata
    sudo mkdir -p "$PEGASUS_THEME_DIR/steam"
    sudo cp "${TPL_DIR}/pegasus/collections/steam/metadata.txt" "$PEGASUS_THEME_DIR/steam/metadata.txt"

    # Copy Apps collection metadata
    sudo mkdir -p "$PEGASUS_THEME_DIR/apps"
    sudo cp "${TPL_DIR}/pegasus/collections/apps/metadata.txt" "$PEGASUS_THEME_DIR/apps/metadata.txt"

    # Install XboxOS theme
    echo "Installing XboxOS theme..."
    XBOXOS_URL=$(curl -s https://api.github.com/repos/alfredolvera/XboxOS/releases/latest | grep "browser_download_url.*\\.zip" | cut -d '"' -f 4)
    if [ -n "$XBOXOS_URL" ]; then
        TEMP_DIR=$(mktemp -d)
        wget -q -O "$TEMP_DIR/xboxos.zip" "$XBOXOS_URL"
        unzip -q "$TEMP_DIR/xboxos.zip" -d "$TEMP_DIR"
        sudo rm -rf "$PEGASUS_THEME_DIR/XboxOS"
        sudo mv "$TEMP_DIR"/* "$PEGASUS_THEME_DIR/XboxOS"
        rm -rf "$TEMP_DIR"
        echo "XboxOS theme installed successfully."
    else
        echo "Warning: Could not download XboxOS theme. Will use default theme."
    fi

    sudo chown -R "$KIOSK_USER:$KIOSK_USER" "/home/$KIOSK_USER/.config"

    if ! id "$KIOSK_USER" &>/dev/null; then
        echo "Creating user '$KIOSK_USER'..."
        sudo useradd -m "$KIOSK_USER"
        echo "Setting empty password for '$KIOSK_USER'..."
        sudo passwd -d "$KIOSK_USER"
    else
        echo "User '$KIOSK_USER' already exists. Skipping creation."
    fi

    autologin_conf="/etc/systemd/system/getty@.service.d/override.conf"
    if [ -f "$autologin_conf" ] && grep -q "$KIOSK_USER" "$autologin_conf"; then
        echo "Autologin already configured. Skipping."
    else
        echo "Configuring autologin for user '$KIOSK_USER'..."
        sudo mkdir -p "$(dirname "$autologin_conf")"
        echo -e "[Service]\nExecStart=\nExecStart=-/sbin/agetty --autologin $KIOSK_USER --noclear %I \$TERM\nType=idle" | sudo tee "$autologin_conf" > /dev/null
    fi

    KIOSK_SCRIPT="/home/$KIOSK_USER/start_pegasus_kiosk.sh"
    if [ -f "$KIOSK_SCRIPT" ]; then
        echo "Kiosk launch script already exists. Skipping."
    else
        echo "Setting up Pegasus launch script..."
        sudo mkdir -p "/home/$KIOSK_USER/"
        sudo cp "${TPL_DIR}/start_pegasus_kiosk.sh" "$KIOSK_SCRIPT"
        sudo chmod +x "$KIOSK_SCRIPT"
        sudo chown "$KIOSK_USER:$KIOSK_USER" "$KIOSK_SCRIPT"
    fi

    PROFILE_FILE="/home/$KIOSK_USER/.bash_profile"
    if [ -f "$PROFILE_FILE" ] && grep -q "start_pegasus_kiosk.sh" "$PROFILE_FILE"; then
        echo "User profile already configured. Skipping."
    else
        echo "Configuring user profile for kiosk mode..."
        sudo cat "${TPL_DIR}/steam_kiosk_profile" >> "$PROFILE_FILE"
        sudo chown "$KIOSK_USER:$KIOSK_USER" "$PROFILE_FILE"
    fi

    echo "Pegasus Kiosk setup complete!"

setup-all:
    @ujust install-default-brew
    @ujust install-default-flatpaks
    @ujust clone-dotfiles
    @ujust configure-git-identity
    @ujust set-default-shell
    @ujust setup-tailscale
    @ujust setup-media-services

[group('Setup')]
setup-interactive:
    #!/usr/bin/env bash
    set -euxo pipefail

    choices=$(gum choose --no-limit \
        "install-default-brew" \
        "install-default-flatpaks" \
        "login-gh" \
        "clone-dotfiles" \
        "configure-git-identity" \
        "set-default-shell" \
        "setup-tailscale" \
        "setup-media-services-interactive" \
        "setup-pegasus-kiosk")

    for choice in $choices; do
        ujust $choice
    done

[group('Debug')]
debug-quadlet:
    #!/usr/bin/env bash
    set -euo pipefail
    sudo /usr/lib/systemd/system-generators/podman-system-generator --dryrun
